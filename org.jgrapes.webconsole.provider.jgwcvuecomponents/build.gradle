dependencies {
	
	compile project(':org.jgrapes.webconsole.base')
    runtimeOnly project(':org.jgrapes.webconsole.provider.vue')
    
}

ext.releaseVersion = "0.4.0"
ext.isSnapshot = false
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

jar {
    into('META-INF') {
        from 'resources/META-INF'
    }
}

apply plugin: 'com.moowork.node'

task prepareNpmLibs(type: Copy) {
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    dependsOn(":jgwc-vue-components:assemble")
    into('build/generated/resources/org/jgrapes/webconsole/provider/jgwcvuecomponents/jgwc-vue-components')
    from ("${rootProject.projectDir}/jgwc-vue-components/") {
        include 'lib/**/*'
        include 'src/**/*'
    }
}

processResources.dependsOn 'prepareNpmLibs'

eclipse {
    autoBuildTasks prepareNpmLibs
}

sourceSets.main.resources.srcDir 'build/generated/resources'

ext.jsDocumentJsDir = "${rootProject.buildDir}/javadoc/org/jgrapes/webconsole/provider/jgwcvuecomponents/jsdoc"

task documentjs(type: NodeTask) {
    dependsOn(':npmInstall')
    new File(jsDocumentJsDir).mkdirs()
    script = file("${rootProject.projectDir}/node_modules/documentation/bin/documentation.js")
    def input = "resources/org/jgrapes/webconsole/provider/jgwcvuecomponents/jgwc-vue-components/jgwc-components.js"
    inputs.file(file(input))
    args = [
        "build",
        "-f", "html",
        "-o", jsDocumentJsDir, "--shallow",
        input
    ]
}

javadoc {
    if (!isJitPackBuild) {
        // Does not work on jitpack
        finalizedBy documentjs
    }
}

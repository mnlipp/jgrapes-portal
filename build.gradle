import aQute.bnd.osgi.Processor
import java.util.regex.Pattern

buildscript {
    repositories {
        jcenter()
        mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.0.0'
    }
}

plugins {
	id "maven-publish"
	id "signing"
	id "org.jdrupes.mdoclet" version "1.0.10"
	id "com.moowork.node" version "1.3.1"
    id 'org.ajoberstar.grgit' version '4.0.1'
	id 'org.ajoberstar.git-publish' version '2.1.1'
    id "com.jfrog.bintray" version "1.8.4"
    id 'io.miret.etienne.sass' version '1.1.1'
}

// Makes Eclipse-groovy handle *.gradle file in buildSrc properly
apply plugin:'groovy'

ext {
	isCiBuild = System.getenv().get("CI") == 'true'
    isJitPackBuild = System.getenv().get("JITPACK") == 'true'
}

// Prepare github authentication for plugins
if (System.properties['org.ajoberstar.grgit.auth.username'] == null) {
	System.setProperty('org.ajoberstar.grgit.auth.username',
		System.getenv("GH_TOKEN") ?: project.properties['github.token'] ?: "nouser")
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        // Sometimes, synchronization with central is too slow
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        // Snapshots
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        // Experimental, not sure if this is the way tpo go...
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    if (project.name.startsWith('org.jgrapes.') 
            || project.name == 'WebConsoleDemo') {
        apply from: "${project.rootDir}/gradle/subprojects.gradle"
    }
}

// Don't know why this task exists in the first place...
project.tasks.bintrayUpload.enabled = false

configurations {
    javadocTaglets
}

dependencies {
    javadocTaglets "org.jdrupes.taglets:plantuml-taglet:1.0.+"
    // javadocTaglets "com.github.mnlipp:jdrupes-taglets:master-SNAPSHOT"
}

gitPublish {
	repoUri = 'https://github.com/mnlipp/jgrapes.git'
	branch = 'gh-pages'
	contents {
		from("${buildDir}/javadoc") {
			into 'javadoc-webconsole'
		}
	}
	preserve { include '**/*' }
	commitMessage = "Updated."
}

// Until https://github.com/ajoberstar/gradle-git-publish/issues/41 is fixed
tasks.gitPublishCopy.dependsOn(":org.jgrapes.webconsole.base:javadoc")
tasks.gitPublishCopy.dependsOn(":org.jgrapes.webconsole.base:jsdoc")

task stage {
    description = 'To be executed by travis, build and update JavaDoc.'
    group = 'build'

    // Build everything first
    subprojects.tasks.each { tc -> tc.findByName("build") }

    if (!isCiBuild || JavaVersion.current().isJava8()) {
        // Publish JavaDoc
        dependsOn gitPublishPush
    }	
}

configure(stage) {
	description = 'To be executed by travis, build and update JavaDoc.'
	group = 'build'
}

apply plugin: 'eclipse'

tasks.eclipse.dependsOn(cleanEclipse)

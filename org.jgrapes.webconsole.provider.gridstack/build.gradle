dependencies {
	
	compile project(':org.jgrapes.webconsole.base')
	
}

ext.releaseVersion = "0.7.0"
ext.isSnapshot = false
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

jar {
    into('META-INF') {
        from 'resources/META-INF'
    }
    into('.') {
        from 'build/generated/resources'
    }
}

task prepareNpmLibs(type: Copy) {
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/provider/gridstack/gridstack')
    from ("${rootProject.projectDir}/node_modules/gridstack/dist/") {
        include '**/*'
        exclude 'src/**'
        exclude 'jquery.*'
        exclude 'jquery-ui.*'
    }
}

processResources.dependsOn ':npm_install'
processResources.dependsOn 'prepareNpmLibs'

sourceSets.main.resources.srcDir 'build/generated/resources'

eclipse {
    autoBuildTasks prepareNpmLibs
}
